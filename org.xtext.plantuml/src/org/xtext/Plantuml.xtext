// Class for generating the grammar for the PlantUML language.
grammar org.xtext.Plantuml 
	//with org.eclipse.xtext.common.Terminals
	hidden (SL_COMMENT, ML_COMMENT)
generate plantuml "http://www.xtext.org/Plantuml"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore


// Model is a number of @startuml's and @enduml's
Plantuml:
	(diagrams+=Diagram)*;
	
// A diagram starts with @Startuml and ends with @enduml, with some number of instructions in between
Diagram:
	START NEWLINE (instructions+=Instruction)* END NEWLINE*
;

// An instruction can be any of the rules for single- and multiline commands.
Instruction: 
	(Arrow
	|Definition (WS Colors)? WS?
	| AutoNumber WS?
	| Title WS?
	| Legend WS?
	| Newpage WS?
	| GroupingMessages WS?
	| Note WS?
	| Divider WS?
	| Reference 
	| Delay WS?
	| Space WS?
	| Hidefootbox WS?
	| Activate WS?
	| Deactivate WS?
	| ParticipantCreation WS?
	| Box WS?
	| IncOut WS?
	| Skinparam WS?)? NEWLINE
;

// Rule for creating sequences between actors (Not necessarily previously defined)
Arrow:
	names+=MyID WS SEQUENCE WS names+=MyID (WS? '!')? (WS? ':' (WS? MyID)*)?
;

// Rule for defining objects in PlantUML, such as
// actors, boundaries, controls, entities and databases
Definition:
	'actor' WS name=MyID 
	| 'boundary' WS name=MyID
	| 'control' WS name=MyID
	| 'entity' WS name=MyID
	| 'database' WS name=MyID
	| 'participant' WS (name=MyID|('"' (WS? MyID)* '"' WS 'as' WS MyID)) ('<<' ('(' MyID ','('#' HEXCODE)')')? MyID* '>>')?
;

Declaration: Definition|Arrow;

// ID rule without Endlegend.
EndLegendID:
	ID |INT| 'create'|'autonumber'|'title'|'legend'|'right'|'left'|'center'
	   |'alt'|'else'|'newpage'|'opt'|'loop'|'par'|'break'|'as'
	   |'critical'|'group'|'note'|'over'|'of'|'ref'|'hide'|'footbox'|'activate'|'database'
	   |'deactivate'|'destroy'|'box'|'#'|','|'('|')'|'='|'-'|'.'|':'|'>'|'<'|'>>'|'<<'|'?'|'!'|'/'|'\\'
	   | '&&'| '&'|'in'|'|'|'||'|'skinparam'|'sequence'|'{'|'}' // |'"'
	   ;

// Using this instead of ID, so that you can use keywords in regular text.
EndID:
	ID |INT| 'create'|'autonumber'|'title'|'legend'|'right'|'left'|'center'
	   |'endlegend'|'alt'|'else'|'newpage'|'opt'|'loop'|'par'|'break'|'as'
	   |'critical'|'group'|'note'|'over'|'of'|'ref'|'hide'|'footbox'|'activate'|'database'
	   |'deactivate'|'destroy'|'box'|'#'|','|'('|')'|'='|'-'|'.'|':'|'>'|'<'|'>>'|'<<'|'?'|'!'|'/'|'\\'
	   | '&&'| '&'|'in'|'|'|'||'|'skinparam'|'sequence'|'{'|'}' // |'"'
	   ;

MyID: EndID|'end';

// Rule for the autonumbering function. Can be followed by one or two numbers, separated by spaces
AutoNumber:
	'autonumber' (WS INT (WS INT)?)? (WS '"' EndID* '"')?
;

// Rule for the title of the diagram
Title:
	'title' (WS MyID)*
;

// Rule for legends.
Legend:
	'legend' WS (('right' | 'left' | 'center') WS)? (NEWLINE (EndLegendID|WS)*)* 'endlegend'
;

// Rule for newpages
Newpage:
	'newpage' WS ANY_OTHER*
;

Else:
	'else' WS MyID* NEWLINE (instructions+=Instruction)*
;

// Rule for "grouping-messages"
GroupingMessages:
	('opt'|'loop'|'par'|'break'|'critical'|'group'|'alt') WS  MyID* NEWLINE (instructions+=Instruction)* (elses+=Else)* 'end'
;

// Rule for notes
Note:
	'note' WS (('right' WS ('of' rid=[Declaration|MyID] (',' rids+=[Declaration|MyID])*)?)
		   		 |('left' WS ('of' lid=[Declaration|MyID] (',' lids+=[Declaration|MyID])*)?)
		  		 |('over' WS oid=[Declaration|MyID](',' oids+=[Declaration|MyID])*)) 
		 		  (Colors)? ((':' (WS? MyID)*)| ((NEWLINE (WS? EndID)*)* 'end note'))
;

// Rule for Dividers.
Divider:
	'==' WS? (MyID WS?)* '=='
;

// Rule for References
Reference:
	'ref over' WS (id=[Declaration|MyID]) (',' WS ids+=[Declaration|MyID])* ((WS? ':' (WS MyID*)*)|((NEWLINE (WS? EndID)*)* 'end ref'))
;

// Rule for delays
// TODO: Read full strings even when containing dots (....).
Delay:
	'...' (WS? (MyID WS?)* '...')?
;

// Rule for Spaces
Space:
	('|||'|('||' INT '||'))
;

// Rule for hiding the footbox
Hidefootbox:
	'hide footbox'
;

// Rules for activation, deactivation and destruction of lifelines
Activate:
	'activate' WS? (Colors WS)? name=MyID
;

Deactivate:
	('deactivate'|'destroy') WS id=[Activate|MyID]
;

// Rule for Participant Creation
// TODO: Make sure the ID hasn't been created in beforehand.
ParticipantCreation:
	'create' WS  MyID
;

// Rule for box/end box
Box:
	'box' WS '"' EndID* '"' (WS Colors)? NEWLINE
	(definitions+=Definition? NEWLINE)* 
	'end box'
;

// Rule for incoming and outgoing messages
IncOut:
	((INCARROW name = MyID)|(name = MyID OUTARROW)) (':' MyID*)?
;

Skinparam:
	'skinparam' WS 'sequence' '{' NEWLINE (SkinparamCommand? NEWLINE)* '}'
;

SkinparamCommand:
	'ArrowColor' Colors
	|'ActorBorderColor' Colors
	|'LifeLineBorderColor' Colors
	|'LifeLineBackgroundColor' Colors
	|'ParticipantBorderColor' Colors
	|'ParticipantBackgroundColor' Colors
	|'ParticipantFontName' ID
	|'ActorBackgroundColor' Colors
	|'ActorFontColor' Colors
	|'Actorfontsize' INT
	|'ActorFontName' ID
;
Colors: '#' (Color|HEXCODE);

Color:
	('AliceBlue'|'AntiqueWhite'|'Aqua'|'Aquamarine'|'Azure'|'Beige'|'Bisque'|'Black'|'BlanchedAlmond'
	|'Blue'|'BlueViolet'|'Brown'|'BurlyWood'|'CadetBlue'|'Chartreuse'|'Chocolate'|'Coral'
	|'CornflowerBlue'|'Cornsilk'|'Crimson'|'Cyan'|'DarkBlue'|'DarkCyan'|'DarkGoldenRod'|'DarkGray'
	|'DarkGreen'|'DarkGrey'|'DarkKhaki'|'DarkMagenta'|'DarkOliveGreen'|'DarkOrchid'|'DarkRed'
	|'DarkSalmo'|'DarkSeaGreen'|'DarkSlateBlue'|'DarkSlateGray'|'DarkSlateGrey'|'DarkTurquoise'
	|'DarkViolet'|'Darkorange'|'DeepPink'|'DeepSkyBlue'|'DimGray'|'DimGrey'|'DodgerBlue'
	|'FireBrick'|'FloralWhite'|'ForestGreen'|'Fuchsia'|'Gainsboro'|'GhostWhite'|'Gold'
	|'GoldenRod'|'Gray'|'Green'|'GreenYellow'|'Grey'|'HoneyDew'|'HotPink'|'IndianRed'
	|'Indigo'|'Ivory'|'Khaki'|'Lavender'|'LavenderBlush'|'LawnGreen'|'LemonChiffon'
	|'LightBlue'|'LightCoral'|'LightCyan'|'LightGoldenRodYellow'|'LightGray'|'LightGreen'
	|'LightGrey'|'LightPink'|'LightSalmon'|'LightSeaGreen'|'LightSkyBlue'|'LightSlateGray'
	|'LightSlateGrey'|'LightSteelBlue'|'LightYellow'|'Lime'|'LimeGreen'|'Linen'|'Magenta'
	|'Maroon'|'MediumAquaMarine'|'MediumBlue'|'MediumOrchid'|'MediumPurple'|'MediumSeaGreen'
	|'MediumSlateBlue'|'MediumSpringGreen'|'MediumTurquoise'|'MediumVioletRed'
	|'MidnightBlue'|'MintCream'|'MistyRose'|'Moccasin'|'NavajoWhite'|'Navy'|'OldLace'
	|'Olive'|'OliveDrab'|'OrangeRed'|'Orchid'|'PaleGoldenRod'|'PaleGreen'
	|'PaleTurquoise'|'PaleVioletRed'|'PapayaWhip'|'PeachPuff'|'Peru'|'Pink'|'Plum'
	|'PowderBlue'|'Purple'|'Red'|'RosyBrown'|'RoyalBlue'|'SaddleBrown'
	|'Salmon'|'SandyBrown'|'SeaGreen'|'Sienna'|'Silver'|'SkyBlue'|'SlateBlue'
	|'SlateGray'|'SlateGrey'|'Snow'|'SpringGreen'|'SteelBlue'|'Tan'|'Teal'|'Thistle'
	|'Tomato'|'Turquoise'|'Violet'|'Wheat'|'White'|'WhiteSmoke'|'Yellow'|'YellowGreen')
;

// Terminals
//--------------------------------------------------

terminal NEWLINE 	: ('\r'? '\n');

terminal START		: '\u0040' 'startuml';

terminal END		: '\u0040' 'enduml';

// Multiline comment begins with /', and ends with '/
terminal ML_COMMENT : '/\'' -> '\'/'; 

// Singleline comment begins with ', and continues until end of line.
terminal SL_COMMENT : '\'' !('\n'|'\r')* ('\r'? '\n')?;

terminal INCARROW: ('[' '-'+ '>')|('[' '<' '-'+);
terminal OUTARROW: ('<' '-'+ ']')|('-'+ '>' ']');

// Sequences are arrows connecting two actors. Has to be defined in unicode.
terminal SEQUENCE:
	('\u002D')* '\u002D' '\u003E'							// - = u002D  | > = u003E
	| '\u003C' '\u002D' ('\u002D')* 						// < = u003C  | - = u002D
	| '\u002D' '\u003E' 'x'									// - = u002D  | > = u003E
	| 'x' '\u003C' '\u002D' 								// < = u003C | - = u002D
	| ('\u002D')* '\u002D' '\u003E' '\u003E'				// - = u002D | > = u003E
	| '\u003C' '\u003C' '\u002D' ('\u002D')*				// < = u003C | - = u002D
	| ('\u002D')* '\u002D' '\u005C' '\u005C'				// - = u002D
	| '\u002F' '\u002D' ('\u002D')*							// / = u002F | - = u002D
	| '\u002F' '\u002F' '\u002D' ('\u002D')*				// / = u002F | - = u002D
	| ('\u002D')* '\u002D' '\u003E' 'o'						// - = u002D | > = u003E
	| 'o' '\u003C' '\u002D' ('\u002D')*						// < = u003C | - = u002D
	| ('\u002D')* '\u002D' '\u002D' '\u002F' '\u002F' 'o'	// - = u002D | / = u002F
	| 'o' '\\\\' '\u002D' '\u002D' ('\u002D')* 				// - = u002D
	| '\u003C' '\u002D' '\u003E' 							// < = u003C | - = u002D | > = u003E
	| '\u003C' '\u002D' '\u003E' 'o' 						// < = u003C | - = u002D | > = u003E
	| 'o' '\u003C' '\u002D' '\u003E' 						// < = u003C | - = u002D | > = u003E
	| '\u003C' '\u007C' '\u002D' '\u002D' 					// < = u003C | | = u007C | - = u002D
	| '\u002D' '\u002D' '\u007C' '\u003E' 					// - = u002D | | = u007C | > = u003E
	| '\u002E' '\u002E' 									// . = u002E
	| '\u002E'												// . = u002E	
;

// INT is a sequence of numbers 0-9.
terminal INT returns ecore::EInt: ('0'..'9')+;

terminal HEXCODE: ('A' .. 'F'|'0' .. '9'|'a' .. 'f')('A' .. 'F'|'0' .. '9'|'a' .. 'f')+;

// ID can be any following sequence of letters and numbers, without spaces between them.
terminal ID  		: ('a'..'z'|'A'..'Z'|'0'..'9' | 'å' | 'ä' | 'ö')*;

terminal WS			: (' '|'\t')+;

terminal ANY_OTHER: .;


